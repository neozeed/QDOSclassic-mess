1000 DIM Dev$(5),V$(4),Dat$(20)
1010 Dev$="Flp1_":V$=VER$
1020 True%=NOT(0):False%=NOT(True%)
1030 REMark -- PDTK TOOLKIT --
1040 _Base=RESPR(3420):LBYTES Dev$&"PDTK_rext",_Base:CALL _Base
1050 REMark -- TURBO TOOLKIT --
1060 REMark * TURBO keywords must be
1070 REMark   initialised before other
1080 REMark   toolkit keywords with the
1090 REMark   same name.
1100 _Base=RESPR(7594):LBYTES Dev$&"TURBO_TK_code",_Base:CALL _Base
1110 REMark -- INITIALISE PDTK TOOLKIT --
1120 PDTK_EXT
1130 REMark -- CHECK IF ROM AREA FREE --
1140 SoftROM%=False%
1150 IF PEEK_L(49152)<>HEX("4AFB0001") THEN 
1160  Arbitrary=HEX("ABBA1978")
1170  POKE_L 49152,Arbitrary
1180  IF PEEK_L(49152)=Arbitrary THEN 
1190   POKE_L 49152,0
1200   SoftROM%=True%
1210  END IF 
1220 END IF 
1230 REMark -- TK2 TOOLKIT --
1240 IF WHERE("TK2_EXT")>0 THEN 
1250  TK2_EXT
1260  TOOLKILL
1270 ELSE 
1280  IF SoftROM% AND FTEST(Dev$&"TK2_rom")=0 THEN 
1290   LBYTES Dev$&"TK2_rom",49152
1300   RESET
1310  ELSE 
1320   INSTALL(Dev$&"TK2_cde")
1330   IF WHERE("TK2_ext")>0 THEN 
1340    TK2_EXT
1350    TOOLKILL
1360   END IF 
1370  END IF 
1380 END IF 
1390 REMark -- SOFTROM TOOLKIT --
1400 IF WHERE("SOFTROM")=0 THEN 
1410  INSTALL(Dev$&"SOFTROM_rext")
1420 END IF 
1430 REMark -- D68K TOOLKIT --
1440 INSTALL(Dev$&"D68K_rext")
1450 REMark -- QLIB LIBRARIES --
1460 INSTALL(Dev$&"QLIB_runtimes")
1470 REMark -- DIY TOOLKIT --
1480 INSTALL(Dev$&"DIY_code")
1490 REMark -- SPEEDSCREEN --
1500 INSTALL(Dev$&"SPEEDSCREEN_code")
1510 REMark -- INSTALL RAM DISK --
1520 IF FREE_MEM > 131072 THEN 
1530  IF DDTEST("RAM") THEN 
1540   INSTALL(Dev$&"DYNAMIC_RAMDRIVE_cde")
1550   IF DDTEST("RAM") THEN 
1560    INSTALL(Dev$&"RAMDISC_cde")
1570    IF DDTEST("RAM")=0 THEN 
1580     FORMAT "ram1_128"
1590    END IF 
1600   END IF 
1610  END IF 
1620 END IF 
1630 REMark -- DATE AND TIME --
1640 Dat$=DATE$
1650 IF Dat$(1 TO 4)="1961" THEN 
1660  IF FTEST(Dev$&"SETDATE_task")=0 THEN 
1670   EXEC_W Dev$&"SETDATE_task"
1680  END IF 
1690 END IF 
1700 REMark -- SYSTEM INFO --
1710 MODE 512
1720 Dat$=DATE$
1730 CLS#0:CLS#2:CLS:PRINT\" QDOS version ";QDOS$\" BASIC version ";VER$\\" ";DAY$;" ";Dat$(10 TO 11);Dat$(5 TO 9);Dat$(1 TO 4);\\
1740 IF SoftROM% THEN PRINT " ROM area 49152 to 65536 free":END IF 
1750 PRINT " ";FREE_MEM;" bytes available"\\
1760 STOP
1770 :
1780 REMark ---- INSTALL A TOOLKIT ----
1790 DEFine PROCedure INSTALL(file$)
1800  LOCal size
1810  IF FTEST(file$)=0 THEN 
1820   LRESPR(file$)
1830   TOOLKILL
1840  END IF 
1850 END DEFine INSTALL
