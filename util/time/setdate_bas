100 DIM Dat$(20),Mon$(36),M$(3)
110 Dat$=DATE$
120 Mon$="JanFebMarAprMayJunJulAugSepOctNovDec"
130 Y%=Dat$(1 TO 4):M$=Dat$(6 TO 8):Mn%=((M$ INSTR Mon$)+2)/3:D%=Dat$(10 TO 11)
140 H%=Dat$(13 TO 14):M%=Dat$(16 TO 17):S%=Dat$(19 TO 20)
150 OPEN#4;"Con_512x256a0x0"
160 PAPER#4;228:INK#4;7:CLS#4
170 BANNER -1,10,7,4,"¼ ½ Select ¾ Increase ¿ Reduce"
180 BANNER -1,50,7,210,"TIME: hh mm ss"
190 BANNER 190,90,7,210,NUM$(H%,2)
200 BANNER 238,90,7,210,NUM$(M%,2)
210 BANNER 286,90,7,210,NUM$(S%,2)
220 BANNER -1,130,7,210,"DATE: day month year"
230 BANNER 172,170,7,210,NUM$(D%,2)
240 BANNER 220,170,7,210,M$
250 BANNER 280,170,7,210,NUM$(Y%,4)
260 BANNER -1,210,7,4,"Esc:Abort  ENTER:Set Clock & Quit"
270 F=0
280 REPeat Lup%
290  SELect ON F
300  =0:BANNER_TXT 190,90,7,255,NUM$(H%,2)
310  =1:BANNER_TXT 238,90,7,255,NUM$(M%,2)
320  =2:BANNER_TXT 286,90,7,255,NUM$(S%,2)
330  =3:BANNER_TXT 172,170,7,255,NUM$(D%,2)
340  =4:BANNER_TXT 220,170,7,255,M$
350  =5:BANNER_TXT 280,170,7,255,NUM$(Y%,4)
360  END SELect 
370  K=CODE(INKEY$(#4,-1))
380  SELect ON K
390  =192,200,10,27
400   SELect ON F
410   =0:BANNER_TXT 190,90,7,210,NUM$(H%,2)
420   =1:BANNER_TXT 238,90,7,210,NUM$(M%,2)
430   =2:BANNER_TXT 286,90,7,210,NUM$(S%,2)
440   =3:BANNER_TXT 172,170,7,210,NUM$(D%,2)
450   =4:BANNER_TXT 220,170,7,210,M$
460   =5:BANNER_TXT 280,170,7,210,NUM$(Y%,4)
470   END SELect 
480  END SELect 
490  SELect ON K
500  =10
510   SDATE Y%,Mn%,D%,H%,M%,S%
520   EXIT Lup%
530  =192:F=(F-1) MOD 6
540  =200:F=(F+1) MOD 6
550  =208
560   SELect ON F
570   =0:H%=(H%+1) MOD 24
580   =1:M%=(M%+1) MOD 60
590   =2:S%=(S%+1) MOD 60
600   =3:D%=(D% MOD 31)+1
610   =4:Mn%=(Mn% MOD 12)+1:M$=Mon$(Mn%*3-2 TO Mn%*3)
620   =5:Y%=Y%+1
630   END SELect 
640  =216
650   SELect ON F
660   =0:H%=(H%-1) MOD 24
670   =1:M%=(M%-1) MOD 60
680   =2:S%=(S%-1) MOD 60
690   =3:D%=((D%-2) MOD 31)+1
700   =4:Mn%=((Mn%-2) MOD 12)+1:M$=Mon$(Mn%*3-2 TO Mn%*3)
710   =5:Y%=Y%-1
720   END SELect 
730  =27:EXIT Lup%
740  END SELect 
750 END REPeat Lup%
760 CLOSE#4
770 STOP
780 :
790 DEFine PROCedure BANNER(Xx%,Yy%,Ink%,Pap%,F$)
800  LOCal L%,X%,Y%,W%,H%
810  L%=LEN(F$):W%=12*(L%+1):H%=30
820  IF Xx%<0 THEN X%=(512-W%)/2:ELSE X%=Xx%:END IF 
830  IF Yy%<0 THEN Y%=(256-H%)/2:ELSE Y%=Yy%:END IF 
840  BLOCK#4;W%,H%,X%+6,Y%+4,0
850  BLOCK#4;W%,H%,X%,Y%,0
860  BANNER_TXT Xx%,Yy%,Ink%,Pap%,F$
870 END DEFine 
880 :
890 DEFine PROCedure BANNER_TXT(Xx%,Yy%,Ink%,Pap%,F$)
900  LOCal L%,X%,Y%,W%,H%
910  L%=LEN(F$):W%=12*(L%+1):H%=30
920  IF Xx%<0 THEN X%=(512-W%)/2:ELSE X%=Xx%:END IF 
930  IF Yy%<0 THEN Y%=(256-H%)/2:ELSE Y%=Yy%:END IF 
940  BLOCK#4;W%-4,H%-2,X%+2,Y%+1,Pap%
950  CSIZE#4;2,1:OVER#4;1:INK#4;0
960  CURSOR#4;X%+5,Y%+4:PRINT#4;F$;
970  CURSOR#4;X%+7,Y%+4:PRINT#4;F$;
980  CURSOR#4;X%+5,Y%+6:PRINT#4;F$;
990  CURSOR#4;X%+7,Y%+6:PRINT#4;F$;
1000  INK#4;Ink%
1010  CURSOR#4;X%+6,Y%+5:PRINT#4;F$;
1020 END DEFine 
1030 :
1040 DEFine FuNction NUM$(N%,M%):LOCal N$(32):N$=FILL$("0",M%)&N%:RETurn N$(LEN(N$)-M%+1 TO LEN(N$)):END DEFine 
