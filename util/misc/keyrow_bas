1 REMark Create conversion table for
2 REMark translating ASCII to KEYROW
3 :
4 REMark The organization is (in ASCII order):
5 REMark  CTRL(bit7) SHFT(bit6) ROWnumber(bits5-3) COLnumber(bits2-0)
6 :
100 WINDOW#2;400,196,56,48
110 PAPER#0;7:INK#0;0
120 MODE 8
130 DELETE "mdv1_KEYROW_tbl"
140 OPEN_NEW#4;"mdv1_KEYROW_tbl"
150 FOR i=0 TO 255
160  IF i=3 THEN cs=2:row=2:col=3:GO TO 360
170  IF (i=224 OR i=225) THEN cs=0:row=3:col=1:GO TO 360
180  IF (i=226 OR i=227) THEN cs=2:row=3:col=1:GO TO 360
190  IF (i=228 OR i=229) THEN cs=1:row=3:col=1:GO TO 360
200  IF (i=230 OR i=231) THEN cs=3:row=3:col=1:GO TO 360
210  IF i=249 THEN cs=2:row=0:col=5:GO TO 360
220  IF i=255 THEN cs=0:row=7:col=2:GO TO 360
230  a$=INKEY$(#0,-1)
240  IF CODE(a$)<>i THEN GO TO 230
250  PAUSE 25
260  FOR j=0 TO 7
270   n=KEYROW(j)
280   IF j=7 THEN n=n && 248
290   IF n<>0 THEN row=j:k=n
300  END FOR j
310  FOR j = 0 TO 7
320   IF (k MOD 2) = 1 THEN col=j
330   k = k DIV 2
340  END FOR j
350  cs=KEYROW(7) && 3
360  n$="@" & cs & row & col
370  PRINT i,n$:PRINT#4;n$;
380  IF (i MOD 8) = 7 THEN PRINT:PRINT#4:ELSE PRINT#4;",";:END IF
390  BEEP 500,1
400 END FOR i
410 CLOSE#4
420 STOP
430 :
440 DEFine PROCedure sav
450  DELETE "flp1_KEYROW_bas"
460  SAVE "flp1_KEYROW_bas"
470 END DEFine
